
plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url "${mavenRepositoryBaseURL}/releases"
    }
}

dependencies {
    testImplementation libs.junit.jupiter
    testImplementation 'org.apache.logging.log4j:log4j-core:3.0.0-beta2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	compileOnly 'org.projectlombok:lombok:1.18.34'
	annotationProcessor 'org.projectlombok:lombok:1.18.34'
	
    implementation libs.guava
    implementation 'commons-cli:commons-cli:1.8.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-api:3.0.0-beta2'

    api project(':archiva-rest')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

tasks.named('test') {
    useJUnitPlatform()
}

ext {
    projectVersion = project.hasProperty('projectVersion') ? project.getProperty('projectVersion') : "0.0.1-SNAPSHOT"
    repositoryName = project.hasProperty('repositoryName') ? project.getProperty('repositoryName') : "snapshots"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = projectArtifactId
            groupId = projectGroupId
            version = projectVersion

            from components["java"]
        }
    }

    repositories {
        maven {
            credentials {
                username = mavenUser
                password = mavenPassword
            }

            url = "${mavenRepositoryBaseURL}/${repositoryName}"
        }
    }
}

task getDeps(type: Copy) {
  from sourceSets.main.runtimeClasspath, sourceSets.test.runtimeClasspath
  into 'runtime/'
}
